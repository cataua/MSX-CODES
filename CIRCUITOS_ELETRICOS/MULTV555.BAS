100 REM *******************************
110 REM *                             *
120 REM * PROGRAMA APLICATIVO PARA CA *
130 REM * LCULO DE MULTIVIBRADORES CO *
140 REM * M O CIRCUITO INTEGRADO  555 *
150 REM * VERSAO 3.0 MSX NOVEMBRO  87 *
160 REM * RAUL M. P. FRIEDMAN ******* *
170 REM * CURITIBA * PARANA BRASIL ** *
180 REM * DIGITADO EM JANEIRO 2025 ** *
190 REM * POR ROGERIO CAETANO ******* *
200 REM * JUIZ DE FORA * MG * BRASIL  *
210 REM *                             *
220 REM *******************************
1000 SCREEN 0:WIDTH 40:KEYOFF:VC=2/3:CV=2/3:REM MENU PRINCIPAL
1010 SCREEN 0:PRINT"CIRCUITO INTEGRADO 555 *****************                                        CALCULOS EM CIRCUITOS   MULTIVIBRADORES":PRINT
1020 PRINT"01 - MULTIVIBRADOR ESTAVEL .... CALCULOS"
1030 PRINT"02 - MULTIVIBRADOR MONOESTAVEL  CALCULOS"
1040 PRINT"03 - MULTIVIBRADOR ASTAVEL .... DIAGRAMA"
1050 PRINT"04 - MULTIVIBRADOR MONOESTAVEL  DIAGRAMA":PRINT
1060 INPUT"OPCAO ";OP:IF OP<1 OR OP>4 OR OP-FIX(OP)<>0 THEN GOTO 1000 ELSE ON OP GOTO 1500,3000,3800,3900
1500 SCREEN 0:WIDTH 40:PRINT"01 - VCN = ";:PRINT USING"+##.###^^^^";VC;:PRINT" [ 1 ]"
1510 PRINT"02 - R1 = ";:PRINT USING"+#.###^^^^";R1;:PRINT" [ R1 ]"
1520 PRINT"03 - R2 = ";:PRINT USING"+#.###^^^^";R2;:PRINT" [ R2 ]"
1530 PRINT"04 - C = ";:PRINT USING"+#.###^^^^";C;:PRINT" [ F ]"
1540 PRINT"05 - TL = ";:PRINT USING"+#.###^^^^";TL;:PRINT" [ s ]"
1550 PRINT"06 - TH = ";:PRINT USING"+#.###^^^^";TH;:PRINT" [ s ]"
1560 PRINT"07 - T = ";:PRINT USING"+#.###^^^^";T ;:PRINT" [ s ]"
1570 PRINT"08 - f = ";:PRINT USING"+#.###^^^^";F ;:PRINT" [ Hz ]"
1580 PRINT"09 - D = ";:PRINT USING"+#.###^^^^";D ;:PRINT" [  1 ]":PRINT
1590 PRINT"10 - VCN R1 R2 C TH TL T f D ->"
1600 PRINT"11 - VCN R1 ** C ** ** T * * ->"
1610 PRINT"12 - VCN R1 ** C ** ** * f * ->"
1620 PRINT"13 - VCN ** R2 C ** ** T * * ->"
1630 PRINT"14 - VCN ** R2 C ** ** * f * ->"
1640 PRINT"15 - VCN ** ** C TL TH * * * ->"
1650 PRINT"16 - VCN ** ** C ** ** T * D ->"
1660 PRINT"17 - VCN ** ** C ** ** * f D ->"
1670 PRINT"18 - VCN R1 R2 * ** ** T * * ->"
1680 PRINT"19 - VCN R1 R2 * ** ** * f * ->"
1690 PRINT"20 - *** R1 R2 C ** ** T * * ->":PRINT
1700 OP=21:LOCATE 32,0:PRINT"OPCAO ":LOCATE 32,2:INPUT OP:IF OP<1 OR OP>21 OR OP-FIX(OP)<>0 THEN GOTO 1500 ELSE LOCATE 0,23
1710 ON OP GOTO 1800,1810,1820,1830,1840,1850,1860,1870,1880,2010,2020,2030,2040,2050,2060,2070,2080,2090,2100,2110,1000
1800 INPUT"VCN     ";VC:IF VCN<=0 OR VCN>=1 THEN GOTO 1800 ELSE GOTO 1500
1810 INPUT"R1      ";R1:GOTO 1500
1820 INPUT"R2      ";R2:GOTO 1500
1830 INPUT"C       ";C:GOTO 1500
1840 INPUT"TL      ";TL:GOTO 1500
1850 INPUT"TH      ";TH:GOTO 1500
1860 INPUT"T       ";T:GOTO 1500
1870 INPUT"f       ";F:GOTO 1500
1880 INPUT"D       ";D:GOTO 1500
2000 REM CALCULOS ASTAVEL
2010 K=(1-VC/2)/(1-VC):TH=(R1+R2)*C*LOG(K):TL=R2*C*LOG(2):T=TL+TH:F=1/T:D=TH/T:GOTO 1500
2020 K=(1-VC/2)/(1-VC):R2=(T-R1*C*LOG(K))/(C*(LOG(K)+LOG(2))):GOTO 2000
2030 T=1/F:GOTO 2020
2040 K=(1-VC/2)/(1-VC):R1=(T-R2*C*(LOG(K)+LOG(2)))/(C*LOG(K)):GOTO 2000
2050 T=1/F:GOTO 2040
2060 K=(1-VC/2)/(1-VC):R2=TL/(C*LOG(2)):R1=(TH-R2*C*LOG(K))/(C*LOG(K)):GOTO 2000
2070 TL=(1-D)*T:TH=D*T:GOTO 2060
2080 T=1/F:GOTO 2070
2090 K=(1-VC/2)/(1-VC):C=T/(R1*LOG(K)+R2*LOG(K)+R2*LOG(2)):GOTO 2060
2100 T=1/F:GOTO 2090
2110 KV=EXP((T-R2*C*LOG(2))/((R1+R2)*C)):VC=(1-KV)/(1/2-KV):GOTO 2000
3000 REM MENU DO MULTIVIBRADOR MONOESTAVEL
3010 SCREEN 0:PRINT"CIRCUITO INTEGRADO 555 *****************                                        CALCULOS EM MULTIVIBRADORES MONOESTAVEIS":PRINT
3020 PRINT"01 - VCN = ";:PRINT USING"+#.###^^^^";CV;:PRINT" [  1 ]"
3030 PRINT"02 - R   = ";:PRINT USING"+#.###^^^^";RR;:PRINT" [  R ]"
3040 PRINT"03 - C   = ";:PRINT USING"+#.###^^^^";CC;:PRINT" [  F ]"
3050 PRINT"04 - TW  = ";:PRINT USING"+#.###^^^^";TW;:PRINT" [  s ]"
3060 PRINT"05 - VCN R C ** ->  TW"
3070 PRINT"06 - VCN R * TW ->   C"
3080 PRINT"07 - VCN * R TW ->   R"
3090 PRINT"08 - *** R C TW -> VCN":PRINT
3100 PRINT"09 - MENU    PRINCIPAL":PRINT:PRINT
3110 OP=9:INPUT"OPCAO    ";OP:IF OP<1 OR OP>8 OR OP-FIX(OP)<>0 THEN GOTO 1000
3120 PRINT:ON OP GOTO 3210,3220,3230,3240,3310,3320,3330,3340,1000
3200 REM ENTRADA DE DADOS MONOESTAVEL
3210 INPUT"VCN     ";CV:GOTO 3000
3220 INPUT"R       ";RR:GOTO 3000
3230 INPUT"C       ";CC:GOTO 3000
3240 INPUT"TW      ";TW:GOTO 3000
3300 REM CALCULOS MONOESTAVEL
3310 TW=RR*CC*LOG(1/(1-CV)):GOTO 3000
3320 CC=TW/(RR*LOG(1/(1-CV))):GOTO 3000
3330 RR=TW/(CC*LOG(1/(1-CV))):GOTO 3000
3340 CV=1-EXP(-(TW/(RR*CC))):GOTO 3000
3800 REM DESENHO DO MULTIVIBRADOR ASTAVEL
3810 GOSUB 4100:DRAW"BM28,120U1R1D2L2U2R1D1R20L20U16E2H4E4H4E2U8BM168,24R4D8R24U8R32D8R24U8R4L4D8BD44U4R4L64R4D4U44D16L4R36U16D20U4R28"
3820 OPEN"GRP:" FOR OUTPUT AS #1:PRESET(200,40):PRINT#1,"TH  TL":PRESET(200,64):PRINT#1,"T":PRESET(216,152):PRINT#1,"f=1/T":PRESET(208,168):PRINT#1,"D=TH/T":PRESET(0,184):PRINT#1,"MULTIVIBRADOR ASTAVEL    T=TH+TL"
3830 PRESET(0,72):PRINT#1,"R1":PRESET(0,104):PRINT#1,"R2":CLOSE#1
3840 GOSUB 4000:GOTO 1000
3900 REM DESENHO DO MULTIVIBRADOR MONOESTAVEL
3910 GOSUB 4100:DRAW"BM28,112U32D40R20L56BM200,32D12U8L8R16U4D12U8R48BD12L48D8L8U8L8BD24R8U8R48D8R8BD29L8U16D20U4L48U16D20U4L8"
3920 OPEN"GRP:" FOR OUTPUT AS #1:PRESET(144,24):PRINT#1,"TIN<TW   TIN":PRESET(216,80):PRINT#1,"TW":PRESET(0,72):PRINT#1,"R":PRESET(0,104):PRINT#1,"IN":PRESET(0,184):PRINT#1,"MULTIVIBRADOR MONOESTAVEL ******":CLOSE#1
3930 GOSUB 4000:GOTO 1000
4000 REM SUBROTINA PARA ESCAPE DE TELA GRAFICA ATRAVES DO ACIONAMENTO DA BARRA DE ESPACOS
4010 RT=1:ON STRING GOSUB 4030:STRIG(0) ON
4020 IF RT=1 THEN GOTO 4020 ELSE RETURN
4030 STRIG(0) OFF:RT=0:RETURN
4100 REM SUBROTINA DE DESENHO BASE
4110 SCREEN 2:DRAW"BM24,152R8L4U14R4L8R4BU4R4L8R4U22U1R1D2L2U2R1D1R20U32L20U1R1D2L2U2R1D1U8E2H4E4H4E2U16R52U1R1D2L2U2R1D1U16D32U16R16D12F2G2H2E2F2G2R32D32R32L32D32R16D14R4L8R4BD4R4L8R4D14R4L8R4BU32L16D16L48D16R4L8R4U16L32U80R48"
4120 OPEN"GRP:" FOR OUTPUT AS #1
4130 PRESET(0,0):PRINT#1,"CIRCUITO INTEGRADO 555 *********":PRESET(48,24):PRINT#1,"VCC   RESET":PRESET(56,72):PRINT#1,"7 8 4 3  OUT":PRESET(80,88):PRINT#1,"555"
4140 PRESET(56,104):PRINT#1,"6         V  VCN=VC/VCC":PRESET(0,144):PRINT#1,"C     GND            10nF":CLOSE#1
4150 RETURN



